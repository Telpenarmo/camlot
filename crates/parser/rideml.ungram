Module = Decl*

Decl =
  DefDecl
| OpenDecl
| TypeDecl

DefDecl = 'def' 'Ident' Params DefBody
OpenDecl = 'open' 'Ident' ';'
TypeDecl = 'type' 'Ident' '=' TypeExpr ';'

DefBody =
  '=' Expr ';'
| BlockExpr

TypeExpr =
  TypeIdent
| TypeArrow
| TypeParen

TypeIdent  = 'Ident'
TypeArrow = from:TypeExpr '->' to:TypeExpr
TypeParen = '(' TypeExpr ')'

Stmt =
  ExprStmt
| LetStmt

ExprStmt = Expr ';'
LetStmt = 'let' 'Ident' Params '=' def:Expr ';'

Expr =
  IdentExpr
| LiteralExpr
| AppExpr
| LambdaExpr
| ParenExpr
| BinaryExpr
| BlockExpr

BlockExpr =
  '{'
    statements:Stmt*
    tail_expr:Expr?
  '}'

IdentExpr = 'Ident'
LiteralExpr = Literal
AppExpr = func:Expr arg:Expr
LambdaExpr = ('Î»' | '\\') Params '->' body:Expr
ParenExpr = '(' Expr? ')'

Literal =  'Int' | 'dummy'

Params = Param*
Param = 'Ident' | '(' 'Ident' ':' TypeExpr ')'

BinaryExpr = lhs:Expr InfixSymbol rhs:Expr
InfixSymbol = '+' | '-' | '*' | '/'
